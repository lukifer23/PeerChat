buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:${libs.versions.agp.get()}")
    }
}

plugins {
    id 'com.diffplug.spotless'
    id 'io.gitlab.arturbosch.detekt' apply false
}

spotless {
    format 'misc', {
        target '**/*.md', '**/.gitignore'
        targetExclude '**/build/**', '**/.gradle/**'
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }

    kotlin {
        target '**/*.kt'
        targetExclude '**/build/**', '**/generated/**', 'config/spotless/**'
        ktlint('1.2.1').editorConfigOverride([
                'ktlint_standard_trailing-comma-on-call-site': 'disabled',
                'ktlint_standard_trailing-comma-on-declaration-site': 'disabled'
        ])
        licenseHeaderFile rootProject.file('config/spotless/copyright.kt'), '(package|import|class|object|sealed|data|enum|fun|val|var)'
    }

    kotlinGradle {
        target '**/*.kts'
        targetExclude '**/build/**'
        ktlint('1.2.1')
    }

    format 'cpp', {
        target 'engine/src/main/cpp/**/*.cpp', 'engine/src/main/cpp/**/*.hpp', 'engine/src/main/cpp/**/*.h'
        targetExclude '**/build/**', '**/generated/**', '**/llama/examples/**'
        clangFormat('16.0.4')
    }
}

subprojects { subproject ->
    subproject.tasks.matching { it.name == 'clean' }.configureEach {
        mustRunAfter(rootProject.tasks.named('clean'))
    }

    subproject.pluginManager.withPlugin('org.jetbrains.kotlin.android') {
        subproject.apply plugin: 'io.gitlab.arturbosch.detekt'

        subproject.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = '17'
                freeCompilerArgs += [
                        '-Xjvm-default=all',
                        '-opt-in=kotlin.RequiresOptIn',
                        '-Xskip-metadata-version-check',
                        '-Xinline-classes'
                ]
            }
        }

    subproject.pluginManager.withPlugin('io.gitlab.arturbosch.detekt') {
        subproject.extensions.configure(io.gitlab.arturbosch.detekt.extensions.DetektExtension) { detektExt ->
            detektExt.buildUponDefaultConfig = true
            detektExt.config.setFrom(rootProject.files('detekt.yml'))
            detektExt.autoCorrect = false
            detektExt.parallel = true
        }

        subproject.tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
            jvmTarget = '17'
            reports.html.required.set(true)
            reports.xml.required.set(false)
            reports.txt.required.set(false)
            reports.sarif.required.set(false)
        }
    }
    }

}
